group 'com.bt.atkinssk'
version '1.0-SNAPSHOT'

configurations {
    jarFiles {
        transitive false
    }
}

dependencies {
    jarFiles project(':devcon-docker-demo-java-application')
}

task copyRuntimeLibs(type: Copy) {
    into project.buildDir
    from configurations.jarFiles
    from "docker"
}


task build(type: Exec) {
    def applicationJarFile = configurations.jarFiles.singleFile.name
    logger.info("applicationJarFile : ${applicationJarFile}")

    commandLine "docker",
            "build",
            "--build-arg", "jarFile=${applicationJarFile}",
            "--tag=devcon-docker-demo",
            project.buildDir
}
build.dependsOn copyRuntimeLibs
copyRuntimeLibs.dependsOn ':devcon-docker-demo-java-application:assemble'


task runImage(type: Exec) {
    mustRunAfter build
    commandLine "docker",
            "run",
            "--rm",
            "--name=devcon-docker-demo",
            "--detach",
            "--publish", "63000:8080",
            "devcon-docker-demo"
}

task removeContainer(type: Exec) {
    commandLine "docker",
            "rm",
            "--force",
            "devcon-docker-demo"
}

task logs(type: Exec) {
    commandLine "docker",
            "logs",
            "devcon-docker-demo"
}

task logsFollow(type: Exec) {
    commandLine "docker",
            "logs",
            "--follow",
            "devcon-docker-demo"
}

task runImageWithBuild {
    dependsOn build
    dependsOn runImage
    doFirst {logger.info ("Build and running image")}
    doLast {logger.info ("Finished")}
    logger.info ("Stuff")
}